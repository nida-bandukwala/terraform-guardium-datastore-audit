locals {
  udc_name       = format("%s%s-%s", var.aws_region, var.postgres_rds_cluster_identifier, local.aws_account_id)
  aws_region     = var.aws_region
  aws_account_id = module.common_aws-configuration.aws_account_id
  log_group      = format("/aws/rds/instance/%s/postgresql", var.postgres_rds_cluster_identifier)
}

module "common_aws-configuration" {
  source = "IBM/common/guardium//modules/aws-configuration"
}

locals {
  pg_audit_user = "rds_pgaudit"
}

module "common_rds-postgres-parameter-group" {
  source                          = "IBM/common/guardium//modules/rds-postgres-parameter-group"
  pg_audit_log                    = "none"
  pg_audit_role                   = "rds_pgaudit"
  force_failover                  = var.force_failover
  postgres_rds_cluster_identifier = var.postgres_rds_cluster_identifier
  aws_region                      = var.aws_region
}

data "gdp-middleware-helper_postgres_role_check" "rds_pgaudit_exists" {
  db_name   = var.db_name
  host      = var.db_host
  port      = var.db_port
  username  = var.db_username
  password  = var.db_password
  role_name = local.pg_audit_user
  ssl_mode  = var.ssl_mode
}

resource "postgresql_role" "rds_pgaudit" {
  count = data.gdp-middleware-helper_postgres_role_check.rds_pgaudit_exists.exists ? 0 : 1
  name  = local.pg_audit_user
  login = false
}

# Grant permissions on tables based on the tables variable
# This resource creates grants for the pgaudit role on specified tables
resource "postgresql_grant" "table_permissions" {
  for_each = { for idx, table in var.tables : "${table.schema}.${table.table}" => table }

  database    = var.db_name
  role        = local.pg_audit_user
  schema      = each.value.schema
  object_type = "table"
  objects     = [each.value.table]
  privileges  = each.value.grants

  depends_on = [postgresql_role.rds_pgaudit]
}

module "common_rds-postgres-sqs-registration" {
  count  = var.log_export_type == "SQS" ? 1 : 0
  source = "IBM/common/guardium//modules/rds-postgres-sqs-registration"

  aws_account_id     = local.aws_account_id
  gdp_client_id      = var.gdp_client_id
  gdp_client_secret  = var.gdp_client_secret
  gdp_password       = var.gdp_password
  gdp_username       = var.gdp_username
  gdp_server         = var.gdp_server
  gdp_mu_host        = var.gdp_mu_host
  udc_aws_credential = var.udc_aws_credential
  log_group          = local.log_group
}

module "common_rds-postgres-cloudwatch-registration" {
  count  = var.log_export_type == "Cloudwatch" ? 1 : 0
  source = "IBM/common/guardium//modules/rds-postgres-cloudwatch-registration"

  aws_account_id      = local.aws_account_id
  gdp_client_id       = var.gdp_client_id
  gdp_client_secret   = var.gdp_client_secret
  gdp_password        = var.gdp_password
  gdp_username        = var.gdp_username
  gdp_server          = var.gdp_server
  gdp_mu_host         = var.gdp_mu_host
  udc_aws_credential  = var.udc_aws_credential
  log_group           = local.log_group
  cloudwatch_endpoint = var.cloudwatch_endpoint
  codec_pattern       = var.codec_pattern
  use_aws_bundled_ca  = var.use_aws_bundled_ca
}
